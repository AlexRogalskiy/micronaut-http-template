plugins {
    id "java"
    id "jacoco"
    id "application"
    id "io.micronaut.aot" version "3.1.1"
    id "io.micronaut.application"  version "3.1.1"

    id "org.sonarqube" version "3.3"
    id "com.diffplug.spotless" version "6.1.0"
    id "com.github.johnrengelman.shadow" version "7.1.0"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

group = groupId
version = artifactVersion

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

micronaut {
    version("3.2.3")
    processing {
        incremental(true)
        module(project.name)
        group(project.group)
        annotations("io.goodforgod.micronaut.temlate.*")
    }
    aot {
        version.set("1.0.0-M5")
        convertYamlToJava.set(true)
        optimizeClassLoading.set(true)
        precomputeOperations.set(true)
        optimizeServiceLoading.set(true)
    }
}

dependencies {
    annotationProcessor "io.micronaut.openapi:micronaut-openapi"
    annotationProcessor "io.micronaut:micronaut-inject-java"

    compileOnly "io.micronaut:micronaut-inject-java"
    compileOnly "io.swagger.core.v3:swagger-annotations"

    implementation "io.micronaut:micronaut-http-server-netty"
    implementation "io.micronaut:micronaut-management"
    implementation "io.micronaut.micrometer:micronaut-micrometer-core"
    implementation "io.micronaut.micrometer:micronaut-micrometer-registry-prometheus"

    implementation "io.projectreactor:reactor-core"
    implementation "io.goodforgod:micronaut-management-openapi:1.0.2"
    implementation "org.jetbrains:annotations:22.0.0"
    implementation "ch.qos.logback:logback-classic:1.2.10"

    testAnnotationProcessor "io.micronaut:micronaut-inject-java"

    testImplementation "io.micronaut:micronaut-http-client"
    testImplementation "io.micronaut.test:micronaut-test-core"
    testImplementation "io.micronaut.test:micronaut-test-junit5"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
}

//noinspection GroovyAssignabilityCheck
run {
    environment([
            "SECURITY_ENABLED": "false",
    ])
}

test {
    failFast(true)
    useJUnitPlatform()
    testLogging {
        showStandardStreams(false)
        events("passed", "skipped", "failed")
        exceptionFormat("full")
    }

    environment([
            "SECURITY_ENABLED": "false",
    ])

    jacoco {
        excludes += ["**/Application*"]
    }

    reports {
        html.enabled(false)
        junitXml.enabled(false)
    }
}

spotless {
    java {
        encoding("UTF-8")
        importOrder()
        removeUnusedImports()
        eclipse("4.21.0").configFile("${projectDir}/config/codestyle.xml")
    }
}

mainClassName = "io.goodforgod.micronaut.template.Application"
jar.enabled(true)
shadowJar {
    mergeServiceFiles()
    manifest {
        attributes "Main-Class": mainClassName
        attributes "Implementation-Version": artifactVersion
        attributes "Build-Version": buildNumber
        attributes "Build-Time": java.time.OffsetDateTime.now()
    }
}

artifacts {
    archives shadowJar
}

tasks.withType(JavaCompile) {
    options.encoding("UTF-8")
    options.incremental(true)
    options.fork = true
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.enabled true
        html.destination file("${buildDir}/jacocoHtml")
    }
}

javadoc {
    options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}
